apply plugin: 'groovy'
apply plugin: BuildGradle

apply plugin: 'maven'

configurations {
    deployerJars
}

repositories {
    mavenCentral()
}

dependencies {
  compile gradleApi()
  compile localGroovy()
  
  deployerJars 'org.apache.maven.wagon:wagon-ssh:2.2'
}

group = "jaci.openrio.gradle"
archivesBaseName = "GradleRIO"
version = "1.0.1"

ext.mavenProps = file "../maven.properties"
mavenProps.withReader {
	def prop = new Properties()
	prop.load(it)
	project.ext.mavenProps = new ConfigSlurper().parse prop
}

processResources {
	ant.replace(file: 'release/build.gradle', token: "{GRADLERIO_VERSION}", value: version)
}

uploadArchives {
	repositories.mavenDeployer {
		configuration = configurations.deployerJars
		repository(url: mavenProps.jaci.url) {
			authentication(userName: mavenProps.jaci.user, password: mavenProps.jaci.auth)
		}
		
		pom {
			groupId = project.group
            version = project.version
            artifactId = project.archivesBaseName
            project {
                name project.archivesBaseName
                packaging 'jar'
                description 'GradleRIO Build Plugin '
            }
		}
	}
}

class BuildGradle implements Plugin<Project> {
  void apply(Project project) {
    project.task('release', dependsOn: 'build') << {
      //ant.copy(file:"build/libs/GradleRIO.jar",
      //  todir: "release/gradle/libs")

      ant.zip(destfile: "releases/GradleRIO-${project.version}.zip",
        basedir: "release")
    }
  }
}
